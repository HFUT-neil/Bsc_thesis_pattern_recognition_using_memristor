* SPICE model for equations proposed by Dr. Mika Laiho et al.
* with Biolek window for memristor boundaries
* Connections:
* TE: Top electrode
* BE: Bottom electrode
* XSV: External connection to plot state variable
* that is not used otherwise
.subckt MEM_LAIHO_WINDOW TE BE XSV
.param a1=4e-8 b1=1.2 a2=1.25e-7 b2=1.2 c1=6e-4 d1=2
+c2=6.6e-4 d2=3.8 x0=0. 001 p=1
* Hyperbolic sine IV relationship
.func IVRel(V1,V2) = IF(V1 >= 0, a1*V2*sinh(b1*V1),
+a2*V2*sinh(b2*V1))
* Equation for state variable
.func SV(V1) = IF(V1 >= 0, c1*sinh(d1*V1), c2*sinh(d2*V1))
* Biolek window function
.func f(V1,I1)={1-pow((V1-stp(-I1)),(2*p))}
* Current source representing memristor
Gmem TE BE value = {IVRel(V(TE,BE),V(XSV,0))}
* Circuit to determine value of state variable
Gxsv 0 XSV value = {SV(V(TE,BE))*f(V(XSV,0),I(Gmem))}
Cx XSV 0 {1}
.ic V(XSV) = x0
.ends MEM_LAIHO_WINDOW