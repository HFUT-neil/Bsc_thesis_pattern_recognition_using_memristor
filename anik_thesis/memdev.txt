* Memristor model for many devices
* Connections:
* TE - top electrode
* BE - bottom electrode
* XSV - External connection to plot state variable
* that is not used otherwise
.subckt MEM_YAKOPCIC TE BE XSV
* Fitting parameters to model different devices
* a1, a2, b: Parameters for IV relationship
* Vp, Vn: Pos. and neg. voltage thresholds
* Ap, An: Multiplier for SV motion intensity
* xp, xn: Points where SV motion is reduced
* alphap, alphan: Rate at which SV motion decays
* xo: Initial value of SV
* eta: SV direction relative to voltage
.params a1=0.2 a2=0.2 b=0.05 Vp=1.1 Vn=1.1 Ap=1.9E9
+An=1.9E9 xp=0.675 xn=0.675 alphap=.01 alphan=.01 xo=0.001 eta=1
* Multiplicitive functions to ensure zero state
* variable motion at memristor boundaries
.func wp(V) = ((xp-V)/(1-xp)+1)
.func wn(V) = V/(1-xn)
.func s6(V) = IF(V >= -Vn, 0, -An*(exp(-V) -exp(Vn)))
* Function G(V(t)) - Describes the device threshold
.func G(V) = IF(V <= Vp, s6(V) , Ap*(exp(V) -exp(Vp)))
* Function F(V(t),x(t)) - Describes the SV motion
.func s1(V2) = exp(-alphap*(V2-xp))*((xp-V2)/(1-xp)+1)
.func s2(V2) = IF(V2 >= xp, s1(V2), 1)
.func s3(V2) = exp(alphan*(V2+xn-1))*V2/(1-xn)
.func s4(V2) = IF(V2 <= (1-xn), s3(V2),1)

.func F(V1,V2) = IF(eta*V1 >= 0, s2(V2), s4(V2))
* IV Response - Hyperbolic sine due to MIM structure
.func IVRel(V1,V2) = IF(V1 >= 0, a1*V2*sinh(b*V1), a2*V2*sinh(b*V1))
* Circuit to determine state variable
* dx/dt = F(V(t),x(t))*G(V(t))
Cx XSV 0 {1}
.ic V(XSV) = xo
Gx 0 XSV value={eta*F(V(TE,BE),V(XSV,0))*G(V(TE,BE))}
* Current source for memristor IV response
Gm TE BE value = {IVRel(V(TE,BE),V(XSV,0))}
.ends MEM_YAKOPCIC
